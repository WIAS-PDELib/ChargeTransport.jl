<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>PSC device on 2D domain (Tensor grid). · ChargeTransport.jl</title><meta name="title" content="PSC device on 2D domain (Tensor grid). · ChargeTransport.jl"/><meta property="og:title" content="PSC device on 2D domain (Tensor grid). · ChargeTransport.jl"/><meta property="twitter:title" content="PSC device on 2D domain (Tensor grid). · ChargeTransport.jl"/><meta name="description" content="Documentation for ChargeTransport.jl."/><meta property="og:description" content="Documentation for ChargeTransport.jl."/><meta property="twitter:description" content="Documentation for ChargeTransport.jl."/><script data-outdated-warner src="../../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.050/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.8/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../search_index.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-mocha.css" data-theme-name="catppuccin-mocha"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-macchiato.css" data-theme-name="catppuccin-macchiato"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-frappe.css" data-theme-name="catppuccin-frappe"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-latte.css" data-theme-name="catppuccin-latte"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../../general/">ChargeTransport.jl</a></span></div><button class="docs-search-query input is-rounded is-small is-clickable my-2 mx-auto py-1 px-2" id="documenter-search-query">Search docs (Ctrl + /)</button><ul class="docs-menu"><li><a class="tocitem" href="../../general/">Home</a></li><li><a class="tocitem" href="../../changes/">Changelog</a></li><li><a class="tocitem" href="../../backgroundinfo/">Mathematical drift-diffusion models</a></li><li><span class="tocitem">How to get started</span><ul><li><a class="tocitem" href="../../GaAs/">van Roosbroeck system</a></li><li><a class="tocitem" href="../../PSC/">Perovskite solar cell</a></li></ul></li><li><a class="tocitem" href="../../allindex/">Types, Constructors and Methods</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../Ex101_PIN/">GaAs diode (1D).</a></li><li><a class="tocitem" href="../Ex102_PIN_nodal_doping/">GaAs diode with spatially varying doping (1D).</a></li><li><a class="tocitem" href="../Ex103_PSC_IVMeasurement/">PSC device with ions and different I-V scan protocols (1D).</a></li><li><a class="tocitem" href="../Ex104_PSC_Photogeneration/">PSC device with photogeneration rate (1D).</a></li><li><a class="tocitem" href="../Ex105_PSC_gradedFlux/">Three-layer PSC device with graded interfaces &amp; Ohmic contacts (1D).</a></li><li><a class="tocitem" href="../Ex106_PSC_SurfaceRecombination/">PSC device with surface recombination (1D).</a></li><li><a class="tocitem" href="../Ex107_MoS2_withIons_BarrierLowering/">MoS2 with moving defects and Schottky Barrier Lowering.</a></li><li><a class="tocitem" href="../Ex108_CIGS/">CIGS: stationary with Schottky contacts.</a></li><li><a class="tocitem" href="../Ex109_PSC_NonDimensional/">Nondimensionalized perovskite solar cell.</a></li><li class="is-active"><a class="tocitem" href>PSC device on 2D domain (Tensor grid).</a></li><li><a class="tocitem" href="../Ex202_Laser_simple/">Simple laser structure with 5 layers.</a></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><a class="docs-sidebar-button docs-navbar-link fa-solid fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>PSC device on 2D domain (Tensor grid).</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>PSC device on 2D domain (Tensor grid).</a></li></ul></nav><div class="docs-right"><a class="docs-navbar-link" href="https://github.com/WIAS-PDELib/ChargeTransport.jl" title="Edit source on GitHub"><span class="docs-icon fa-solid"></span></a><a class="docs-settings-button docs-navbar-link fa-solid fa-gear" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-article-toggle-button fa-solid fa-chevron-up" id="documenter-article-toggle-button" href="javascript:;" title="Collapse all docstrings"></a></div></header><article class="content" id="documenter-page"><h1 id="PSC-device-on-2D-domain-(Tensor-grid)."><a class="docs-heading-anchor" href="#PSC-device-on-2D-domain-(Tensor-grid).">PSC device on 2D domain (Tensor grid).</a><a id="PSC-device-on-2D-domain-(Tensor-grid).-1"></a><a class="docs-heading-anchor-permalink" href="#PSC-device-on-2D-domain-(Tensor-grid)." title="Permalink"></a></h1><p>(<a href="../Ex201_PSC_tensorGrid.jl">source code</a>)</p><p>Simulating a three layer PSC device PCBM | MAPI | Pedot with mobile ions. The simulations are performed in 2D on a tensor grid, out of equilibrium and with abrupt interfaces.</p><pre><code class="language-julia hljs">module Ex201_PSC_tensorGrid

using ChargeTransport
using ExtendableGrids
using PyPlot</code></pre><p>you can also use other Plotters, if you add them to the example file you can set verbose also to true to display some solver information</p><pre><code class="language-julia hljs">function main(;
        n = 3, Plotter = PyPlot, plotting = false, verbose = false, test = false,
        parameter_set = Params_PSC_PCBM_MAPI_Pedot, # choose the parameter set
        vacancyEnergyCalculation = true,            # assume the vacancy energy level is either given or not
    )

    ################################################################################
    if test == false
        println(&quot;Define physical parameters and model&quot;)
    end
    ################################################################################

    @local_unitfactors μm cm s ns V K ps Hz W</code></pre><p>parameter</p><pre><code class="language-julia hljs">    p = parameter_set()

    bregionNoFlux = 3
    height = 5.0e-6 * cm

    # contact voltage
    voltageAcceptor = 1.2 * V

    # primary data for I-V scan protocol
    scanrate = 0.4 * V / s
    number_tsteps = 31
    endVoltage = voltageAcceptor # bias goes until the given voltage at acceptor boundary

    # with fixed timestep sizes we can calculate the times a priori
    tend = endVoltage / scanrate
    tvalues = range(0, stop = tend, length = number_tsteps)

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Set up grid and regions&quot;)
    end
    ################################################################################

    δ = 4 * n        # the larger, the finer the mesh
    t = 0.5 * (cm) / δ # tolerance for geomspace and glue (with factor 10)
    k = 1.5        # the closer to 1, the closer to the boundary geomspace

    coord_n_u = collect(range(0.0, p.h_ndoping / 2, step = p.h_ndoping / (0.6 * δ)))
    coord_n_g = geomspace(
        p.h_ndoping / 2, p.h_ndoping,
        p.h_ndoping / (0.7 * δ), p.h_ndoping / (1.1 * δ),
        tol = t
    )
    coord_i_g1 = geomspace(
        p.h_ndoping, p.h_ndoping + p.h_intrinsic / k,
        p.h_intrinsic / (5.1 * δ), p.h_intrinsic / (1.0 * δ),
        tol = t
    )
    coord_i_g2 = geomspace(
        p.h_ndoping + p.h_intrinsic / k, p.h_ndoping + p.h_intrinsic,
        p.h_intrinsic / (1.0 * δ), p.h_intrinsic / (5.1 * δ),
        tol = t
    )
    coord_p_g = geomspace(
        p.h_ndoping + p.h_intrinsic, p.h_ndoping + p.h_intrinsic + p.h_pdoping / 2,
        p.h_pdoping / (1.3 * δ), p.h_pdoping / (0.3 * δ),
        tol = t
    )
    coord_p_u = collect(range(p.h_ndoping + p.h_intrinsic + p.h_pdoping / 2, p.h_ndoping + p.h_intrinsic + p.h_pdoping, step = p.h_pdoping / (0.6 * δ)))

    coord = glue(coord_n_u, coord_n_g, tol = 10 * t)
    coord = glue(coord, coord_i_g1, tol = 10 * t)
    coord = glue(coord, coord_i_g2, tol = 10 * t)
    coord = glue(coord, coord_p_g, tol = 10 * t)
    coord_length = glue(coord, coord_p_u, tol = 10 * t)

    height_L = geomspace(0.0, height / 2, height / (0.4 * δ), height / (0.4 * δ))
    height_R = geomspace(height / 2, height, height / (0.4 * δ), height / (0.4 * δ))
    coord_height = glue(height_L, height_R, tol = 10 * t)

    grid = simplexgrid(coord_length, coord_height)

    # specify inner regions
    cellmask!(grid, [0.0, 0.0], [p.h_ndoping, height], p.regionDonor, tol = 1.0e-18)
    cellmask!(grid, [p.h_pdoping, 0.0], [p.h_ndoping + p.h_intrinsic, height], p.regionIntrinsic, tol = 1.0e-18)
    cellmask!(grid, [p.h_ndoping + p.h_intrinsic, 0.0], [p.h_total, height], p.regionAcceptor, tol = 1.0e-18)

    # specify outer regions
    # metal interfaces
    bfacemask!(grid, [0.0, 0.0], [0.0, height], p.bregionDonor)            # BregionNumber = 1
    bfacemask!(grid, [p.h_total, 0.0], [p.h_total, height], p.bregionAcceptor) # BregionNumber = 2

    # no flux interfaces [xmin, ymin], [xmax, ymax]
    bfacemask!(grid, [0.0, 0.0], [p.h_total, 0.0], bregionNoFlux)          # BregionNumber = 3
    bfacemask!(grid, [0.0, height], [p.h_total, height], bregionNoFlux)    # BregionNumber = 3

    if plotting
        gridplot(grid, Plotter = Plotter, resolution = (600, 400), linewidth = 0.5, legend = :lt)
        Plotter.title(&quot;Grid&quot;)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define System and fill in information about model&quot;)
    end
    ################################################################################

    # Initialize Data instance and fill in data
    data = Data(grid, p.numberOfCarriers)

    # Possible choices: Stationary, Transient
    data.modelType = Transient

    # Possible choices: Boltzmann, FermiDiracOneHalfBednarczyk, FermiDiracOneHalfTeSCA,
    # FermiDiracMinusOne, Blakemore
    data.F = [FermiDiracOneHalfTeSCA, FermiDiracOneHalfTeSCA, FermiDiracMinusOne]

    data.bulkRecombination = set_bulk_recombination(;
        iphin = p.iphin, iphip = p.iphip,
        bulk_recomb_Auger = false,
        bulk_recomb_radiative = true,
        bulk_recomb_SRH = true
    )

    # Possible choices: OhmicContact, SchottkyContact (outer boundary) and InterfaceNone,
    # InterfaceRecombination (inner boundary).
    data.boundaryType[p.bregionAcceptor] = OhmicContact
    data.boundaryType[p.bregionDonor] = OhmicContact

    # Present ionic vacancies in perovskite layer
    enable_ionic_carrier!(data, ionicCarrier = p.iphia, regions = [p.regionIntrinsic])

    # Choose flux discretization scheme: ScharfetterGummel, ScharfetterGummelGraded,
    # ExcessChemicalPotential, ExcessChemicalPotentialGraded, DiffusionEnhanced, GeneralizedSG
    data.fluxApproximation .= ExcessChemicalPotential

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define Params and fill in physical parameters&quot;)
    end
    ################################################################################

    data.params = Params(p)

    if !vacancyEnergyCalculation
        data.params.bandEdgeEnergy[p.iphia, p.regionIntrinsic] = p.Ea[p.regionIntrinsic]
    end

    ctsys = System(grid, data, unknown_storage = :sparse)

    if test == false
        show_params(ctsys)
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Define control parameters for Solver&quot;)
    end
    ################################################################################

    control = SolverControl()
    control.verbose = verbose
    control.maxiters = 300
    control.max_round = 5
    control.damp_initial = 0.1
    control.damp_growth = 1.21 # &gt;= 1

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Compute solution in thermodynamic equilibrium for Boltzmann&quot;)
    end
    ################################################################################

    solution = equilibrium_solve!(ctsys, control = control, vacancyEnergyCalculation = vacancyEnergyCalculation)
    inival = solution

    if plotting # currently, plotting the solution was only tested with PyPlot.
        ipsi = data.index_psi
        X = grid[Coordinates][1, :]
        Y = grid[Coordinates][2, :]

        Plotter.figure()
        Plotter.surf(X[:], Y[:], solution[ipsi, :])
        Plotter.title(&quot;Electrostatic potential \$ \\psi \$ (Equilibrium)&quot;)
        Plotter.xlabel(&quot;length [m]&quot;)
        Plotter.ylabel(&quot;height [m]&quot;)
        Plotter.zlabel(&quot;potential [V]&quot;)
        Plotter.tight_layout()
        ################
        Plotter.figure()
        Plotter.surf(X[:], Y[:], solution[p.iphin, :])
        Plotter.title(&quot;quasi Fermi potential \$ \\varphi_n \$ (Equilibrium)&quot;)
        Plotter.xlabel(&quot;length [m]&quot;)
        Plotter.ylabel(&quot;height [m]&quot;)
        Plotter.zlabel(&quot;potential [V]&quot;)
        Plotter.tight_layout()
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;I-V Measurement Loop&quot;)
    end
    ################################################################################

    # for saving I-V data
    IV = zeros(0) # for IV values
    biasValues = zeros(0) # for bias values

    for istep in 2:number_tsteps

        t = tvalues[istep]       # Actual time
        Δu = t * scanrate         # Applied voltage
        Δt = t - tvalues[istep - 1] # Time step size

        # Apply new voltage; set non equilibrium boundary conditions
        set_contact!(ctsys, p.bregionAcceptor, Δu = Δu)

        if test == false
            println(&quot;time value: t = $(t) s&quot;)
        end

        solution = solve(ctsys, inival = inival, control = control, tstep = Δt)

        # get I-V data
        current = get_current_val(ctsys, solution, inival, Δt)

        push!(IV, current)
        push!(biasValues, Δu)

        inival = solution
    end # time loop

    if plotting
        Plotter.figure()
        Plotter.surf(X[:], Y[:], solution[ipsi, :])
        Plotter.title(&quot;Electrostatic potential \$ \\psi \$ at end time&quot;)
        Plotter.xlabel(&quot;length [m]&quot;)
        Plotter.ylabel(&quot;height [m]&quot;)
        Plotter.zlabel(&quot;potential [V]&quot;)
        ################
        Plotter.figure()
        Plotter.surf(X[:], Y[:], solution[p.iphin, :])
        Plotter.title(&quot;quasi Fermi potential \$ \\varphi_n \$ at end time&quot;)
        Plotter.xlabel(&quot;length [m]&quot;)
        Plotter.ylabel(&quot;height [m]&quot;)
        Plotter.zlabel(&quot;potential [V]&quot;)
        ################
        Plotter.figure()
        Plotter.plot(biasValues, IV .* (cm)^2 / height, label = &quot;&quot;, linewidth = 3, marker = &quot;o&quot;)
        Plotter.grid()
        Plotter.ylabel(&quot;total current [A]&quot;) #
        Plotter.xlabel(&quot;Applied Voltage [V]&quot;)
    end

    if test == false
        integral = integrated_density(ctsys, sol = solution, icc = p.iphia, ireg = p.regionIntrinsic)

        println(&quot; &quot;)
        println(&quot;Calculated average vacancy density is: &quot;, integral / data.regionVolumes[p.regionIntrinsic])
        println(&quot; &quot;)
        if vacancyEnergyCalculation
            vacancyEnergy = data.params.bandEdgeEnergy[p.iphia, p.regionIntrinsic] / data.constants.q
            println(&quot;Value for vacancy energy is: &quot;, vacancyEnergy, &quot; eV. Save this value for later use.&quot;)
            println(&quot;We recommend to calculate it on a fine grid.&quot;)
            println(&quot; &quot;)
        end
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    testval = sum(filter(!isnan, solution)) / length(solution) # when using sparse storage, we get NaN values in solution
    return testval

end #  main

function test()
    testval = -0.5815947701306688; testvalvacancyEnergyCalculation = -0.5823076281125769
    return main(test = true) ≈ testval &amp;&amp; main(test = true, vacancyEnergyCalculation = false) ≈ testvalvacancyEnergyCalculation
end

if test == false
    println(&quot;This message should show when this module is successfully recompiled.&quot;)
end

end # module</code></pre><hr/><p><em>This page was generated using <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a>.</em></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../Ex109_PSC_NonDimensional/">« Nondimensionalized perovskite solar cell.</a><a class="docs-footer-nextpage" href="../Ex202_Laser_simple/">Simple laser structure with 5 layers. »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="auto">Automatic (OS)</option><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option><option value="catppuccin-latte">catppuccin-latte</option><option value="catppuccin-frappe">catppuccin-frappe</option><option value="catppuccin-macchiato">catppuccin-macchiato</option><option value="catppuccin-mocha">catppuccin-mocha</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 1.14.1 on <span class="colophon-date" title="Monday 20 October 2025 13:26">Monday 20 October 2025</span>. Using Julia version 1.12.1.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
