<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>PSC device with ions and different I-V scan protocols (1D). · ChargeTransport.jl</title><meta name="title" content="PSC device with ions and different I-V scan protocols (1D). · ChargeTransport.jl"/><meta property="og:title" content="PSC device with ions and different I-V scan protocols (1D). · ChargeTransport.jl"/><meta property="twitter:title" content="PSC device with ions and different I-V scan protocols (1D). · ChargeTransport.jl"/><meta name="description" content="Documentation for ChargeTransport.jl."/><meta property="og:description" content="Documentation for ChargeTransport.jl."/><meta property="twitter:description" content="Documentation for ChargeTransport.jl."/><script data-outdated-warner src="../../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.050/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.8/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL="../.."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../../assets/documenter.js"></script><script src="../../search_index.js"></script><script src="../../siteinfo.js"></script><script src="../../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-mocha.css" data-theme-name="catppuccin-mocha"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-macchiato.css" data-theme-name="catppuccin-macchiato"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-frappe.css" data-theme-name="catppuccin-frappe"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/catppuccin-latte.css" data-theme-name="catppuccin-latte"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../../general/">ChargeTransport.jl</a></span></div><button class="docs-search-query input is-rounded is-small is-clickable my-2 mx-auto py-1 px-2" id="documenter-search-query">Search docs (Ctrl + /)</button><ul class="docs-menu"><li><a class="tocitem" href="../../general/">Home</a></li><li><a class="tocitem" href="../../changes/">Changelog</a></li><li><a class="tocitem" href="../../backgroundinfo/">Mathematical drift-diffusion models</a></li><li><span class="tocitem">How to get started</span><ul><li><a class="tocitem" href="../../GaAs/">van Roosbroeck system</a></li><li><a class="tocitem" href="../../PSC/">Perovskite solar cell</a></li></ul></li><li><a class="tocitem" href="../../allindex/">Types, Constructors and Methods</a></li><li><span class="tocitem">Examples</span><ul><li><a class="tocitem" href="../Ex101_PIN/">GaAs diode (1D).</a></li><li><a class="tocitem" href="../Ex102_PIN_nodal_doping/">GaAs diode with spatially varying doping (1D).</a></li><li class="is-active"><a class="tocitem" href>PSC device with ions and different I-V scan protocols (1D).</a><ul class="internal"><li class="toplevel"><a class="tocitem" href="#you-can-also-use-other-Plotters,-if-you-add-them-to-the-example-file"><span>you can also use other Plotters, if you add them to the example file</span></a></li></ul></li><li><a class="tocitem" href="../Ex104_PSC_Photogeneration/">PSC device with photogeneration rate (1D).</a></li><li><a class="tocitem" href="../Ex105_PSC_gradedFlux/">Three-layer PSC device with graded interfaces &amp; Ohmic contacts (1D).</a></li><li><a class="tocitem" href="../Ex106_PSC_SurfaceRecombination/">PSC device with surface recombination (1D).</a></li><li><a class="tocitem" href="../Ex107_MoS2_withIons_BarrierLowering/">MoS2 with moving defects and Schottky Barrier Lowering.</a></li><li><a class="tocitem" href="../Ex108_CIGS/">CIGS: stationary with Schottky contacts.</a></li><li><a class="tocitem" href="../Ex109_PSC_NonDimensional/">Nondimensionalized perovskite solar cell.</a></li><li><a class="tocitem" href="../Ex201_PSC_tensorGrid/">PSC device on 2D domain (Tensor grid).</a></li><li><a class="tocitem" href="../Ex202_Laser_simple/">Simple laser structure with 5 layers.</a></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><a class="docs-sidebar-button docs-navbar-link fa-solid fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href>PSC device with ions and different I-V scan protocols (1D).</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>PSC device with ions and different I-V scan protocols (1D).</a></li></ul></nav><div class="docs-right"><a class="docs-navbar-link" href="https://github.com/WIAS-PDELib/ChargeTransport.jl" title="Edit source on GitHub"><span class="docs-icon fa-solid"></span></a><a class="docs-settings-button docs-navbar-link fa-solid fa-gear" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-article-toggle-button fa-solid fa-chevron-up" id="documenter-article-toggle-button" href="javascript:;" title="Collapse all docstrings"></a></div></header><article class="content" id="documenter-page"><h1 id="PSC-device-with-ions-and-different-I-V-scan-protocols-(1D)."><a class="docs-heading-anchor" href="#PSC-device-with-ions-and-different-I-V-scan-protocols-(1D).">PSC device with ions and different I-V scan protocols (1D).</a><a id="PSC-device-with-ions-and-different-I-V-scan-protocols-(1D).-1"></a><a class="docs-heading-anchor-permalink" href="#PSC-device-with-ions-and-different-I-V-scan-protocols-(1D)." title="Permalink"></a></h1><p>(<a href="../Ex103_PSC_IVMeasurement.jl">source code</a>)</p><p>Simulating a three layer PSC device Ti02| MAPI | spiro-OMeTAD with mobile ions where the ion vacancy accumulation is limited by the Fermi-Dirac integral of order -1.</p><p>The time-dependent simulations are performed with abrupt interfaces. Two different I-V measurement protocols are included and the corresponding solution vectors and the I-V curve after the scan can be depicted.</p><pre><code class="language-julia hljs">module Ex103_PSC_IVMeasurement

using ChargeTransport
using ExtendableGrids
using PyPlot</code></pre><h1 id="you-can-also-use-other-Plotters,-if-you-add-them-to-the-example-file"><a class="docs-heading-anchor" href="#you-can-also-use-other-Plotters,-if-you-add-them-to-the-example-file">you can also use other Plotters, if you add them to the example file</a><a id="you-can-also-use-other-Plotters,-if-you-add-them-to-the-example-file-1"></a><a class="docs-heading-anchor-permalink" href="#you-can-also-use-other-Plotters,-if-you-add-them-to-the-example-file" title="Permalink"></a></h1><p>you can set verbose also to true to display some solver information</p><pre><code class="language-julia hljs">function main(;
        n = 3, Plotter = PyPlot, plotting = false, verbose = false, test = false,
        parameter_set = Params_PSC_TiO2_MAPI_spiro, # choose the parameter set
        otherScanProtocol = false,                  # you can choose between two scan protocols
        vacancyEnergyCalculation = true,            # assume the vacancy energy level is either given or not
    )

    @local_unitfactors μm cm s ns V K ps Hz

    if plotting
        Plotter.close(&quot;all&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Define physical parameters and model&quot;)
    end
    ################################################################################</code></pre><p>parameters</p><pre><code class="language-julia hljs">    p = parameter_set()

    # contact voltage
    voltageAcceptor = 1.2 * V

    # primary data for I-V scan protocol
    scanrate = 1.0 * V / s
    endVoltage = voltageAcceptor # bias goes until the given voltage at acceptor boundary
    tend = endVoltage / scanrate

    # Define scan protocol function
    function linearScanProtocol(t)
        return if t == Inf
            0.0
        else
            scanrate * t
        end
    end

    # Apply zero voltage on left boundary and a linear scan protocol on right boundary
    contactVoltageFunction = [zeroVoltage, linearScanProtocol]

    # Instead of a linear scan protocol, we can also apply other scan protocols which we
    # define by our own and parse to the model generator via the struct Data
    if otherScanProtocol
        # scan protocol parameter
        frequency = 10.0 * Hz
        amplitude = 0.2 * V
        tend = 1 / frequency

        # Define sinusoidal applied voltage
        function sinusoidalScanProtocol(t)
            return if t == Inf
                0.0
            else
                amplitude * sin(2.0 * pi * frequency * t)
            end
        end

        # Apply zero voltage on left boundary and a linear scan protocol on right boundary
        contactVoltageFunction = [zeroVoltage, sinusoidalScanProtocol]
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Set up grid and regions&quot;)
    end
    ################################################################################

    δ = 4 * n        # the larger, the finer the mesh
    t = 0.5 * (cm) / δ # tolerance for geomspace and glue (with factor 10)
    k = 1.5        # the closer to 1, the closer to the boundary geomspace

    coord_n_u = collect(range(0.0, p.h_ndoping / 2, step = p.h_ndoping / (0.8 * δ)))
    coord_n_g = geomspace(
        p.h_ndoping / 2, p.h_ndoping,
        p.h_ndoping / (0.7 * δ), p.h_ndoping / (1.1 * δ),
        tol = t
    )
    coord_i_g1 = geomspace(
        p.h_ndoping, p.h_ndoping + p.h_intrinsic / k,
        p.h_intrinsic / (2.8 * δ), p.h_intrinsic / (2.1 * δ),
        tol = t
    )
    coord_i_g2 = geomspace(
        p.h_ndoping + p.h_intrinsic / k, p.h_ndoping + p.h_intrinsic,
        p.h_intrinsic / (2.1 * δ), p.h_intrinsic / (2.8 * δ),
        tol = t
    )
    coord_p_g = geomspace(
        p.h_ndoping + p.h_intrinsic, p.h_ndoping + p.h_intrinsic + p.h_pdoping / 2,
        p.h_pdoping / (1.6 * δ), p.h_pdoping / (1.6 * δ),
        tol = t
    )
    coord_p_u = collect(range(p.h_ndoping + p.h_intrinsic + p.h_pdoping / 2, p.h_ndoping + p.h_intrinsic + p.h_pdoping, step = p.h_pdoping / (1.3 * δ)))

    coord = glue(coord_n_u, coord_n_g, tol = 10 * t)
    coord = glue(coord, coord_i_g1, tol = 10 * t)
    coord = glue(coord, coord_i_g2, tol = 10 * t)
    coord = glue(coord, coord_p_g, tol = 10 * t)
    coord = glue(coord, coord_p_u, tol = 10 * t)
    grid = ExtendableGrids.simplexgrid(coord)

    # set different regions in grid
    cellmask!(grid, [0.0 * μm], [p.heightLayers[1]], p.regionDonor, tol = 1.0e-18)     # n-doped region   = 1
    cellmask!(grid, [p.heightLayers[1]], [p.heightLayers[2]], p.regionIntrinsic, tol = 1.0e-18) # intrinsic region = 2
    cellmask!(grid, [p.heightLayers[2]], [p.heightLayers[3]], p.regionAcceptor, tol = 1.0e-18)  # p-doped region   = 3

    # bfacemask! for setting different boundary regions
    bfacemask!(grid, [0.0], [0.0], p.bregionDonor, tol = 1.0e-18)     # outer left boundary
    bfacemask!(grid, [p.h_total], [p.h_total], p.bregionAcceptor, tol = 1.0e-18)  # outer right boundary
    bfacemask!(grid, [p.heightLayers[1]], [p.heightLayers[1]], p.bregionJ1, tol = 1.0e-18) # first  inner interface
    bfacemask!(grid, [p.heightLayers[2]], [p.heightLayers[2]], p.bregionJ2, tol = 1.0e-18) # second inner interface

    if plotting
        gridplot(grid, Plotter = Plotter, legend = :lt)
        Plotter.title(&quot;Grid&quot;)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define System and fill in information about model&quot;)
    end
    ################################################################################

    # Initialize Data instance and fill in predefined data
    # Currently, the way to go is to pass a contact voltage function exactly here.
    data = Data(grid, p.numberOfCarriers, contactVoltageFunction = contactVoltageFunction)

    # Possible choices: Stationary, Transient
    data.modelType = Transient

    # The default for electrons and holes is Boltzmann. Here, we set it to a more general statistics function
    data.F[p.iphin] = FermiDiracOneHalfTeSCA
    data.F[p.iphip] = FermiDiracOneHalfTeSCA

    data.bulkRecombination = set_bulk_recombination(;
        iphin = p.iphin, iphip = p.iphip,
        bulk_recomb_Auger = false,
        bulk_recomb_radiative = true,
        bulk_recomb_SRH = true
    )

    # Possible choices: OhmicContact, SchottkyContact (outer boundary) and InterfaceNone,
    # InterfaceRecombination (inner boundary).
    data.boundaryType[p.bregionAcceptor] = OhmicContact
    data.boundaryType[p.bregionDonor] = OhmicContact

    # With this method, the user enable the ionic carrier parsed to ionicCarrier and gives
    # gives the information on which regions this ionic carrier is defined.
    # In this application ion vacancies only live in active perovskite layer.
    # by default the statistics function is set to FermiDiracMinusOne to limit ion depletion
    enable_ionic_carrier!(data, ionicCarrier = p.iphia, regions = [p.regionIntrinsic])

    if test == false
        println(&quot;*** done\n&quot;)
    end

    ################################################################################
    if test == false
        println(&quot;Define Params and fill in physical parameters&quot;)
    end
    ################################################################################

    data.params = Params(p)

    if !vacancyEnergyCalculation
        data.params.bandEdgeEnergy[p.iphia, p.regionIntrinsic] = p.Ea[p.regionIntrinsic]
    end

    ctsys = System(grid, data, unknown_storage = :sparse)

    if test == false
        show_params(ctsys)
        println(&quot;*** done\n&quot;)
    end

    if plotting == true
        ################################################################################
        println(&quot;Plot electroneutral potential, band-edge energies and doping&quot;)
        ################################################################################
        label_solution, label_density, label_energy, label_BEE = set_plotting_labels(data)

        # add labels for anion vacancy
        label_energy[1, p.iphia] = &quot;\$E_a-q\\psi\$&quot;; label_energy[2, p.iphia] = &quot;\$ - q \\varphi_a\$&quot;; label_BEE[p.iphia] = &quot;\$E_a\$&quot;
        label_density[p.iphia] = &quot;\$ n_a \$&quot;;      label_solution[p.iphia] = &quot;\$ \\varphi_a\$&quot;

    end
    ################################################################################
    if test == false
        println(&quot;Define control parameters for Solver&quot;)
    end
    ################################################################################

    control = SolverControl()
    control.verbose = verbose
    control.max_round = 5
    control.damp_initial = 0.1
    control.damp_growth = 1.21 # &gt;= 1

    if test == false
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;Compute solution in thermodynamic equilibrium&quot;)
    end
    ################################################################################

    # calculate equilibrium solution and as initial guess
    solution = equilibrium_solve!(ctsys, control = control, vacancyEnergyCalculation = vacancyEnergyCalculation)
    inival = solution

    if plotting
        Plotter.figure()
        plot_energies(Plotter, ctsys, solution, &quot;Equilibrium&quot;, label_energy)
        Plotter.figure()
        plot_densities(Plotter, ctsys, solution, &quot;Equilibrium&quot;, label_density)
        Plotter.figure()
        plot_solution(Plotter, ctsys, solution, &quot;Equilibrium&quot;, label_solution)
    end

    if test == false
        println(&quot;*** done\n&quot;)
    end
    ################################################################################
    if test == false
        println(&quot;IV Measurement&quot;)
    end
    ################################################################################

    control.Δt = 0.5
    control.Δt_grow = 1.0

    if otherScanProtocol
        control.Δt_min = 1.0e-4
        control.Δt = 1.0e-4
        control.Δt_grow = 1.2
    end

    # calculation of solution
    sol = ChargeTransport.solve(ctsys, inival = inival, times = (0.0, tend), control = control)

    tvalues = sol.t
    number_tsteps = length(tvalues)

    # for saving I-V data
    IV = zeros(0)    # for IV values
    ISRHn = zeros(0)
    ISRHp = zeros(0) # for SRH recombination current
    IRadn = zeros(0)
    IRadp = zeros(0) # for radiative recombination current

    for istep in 2:number_tsteps

        Δt = tvalues[istep] - tvalues[istep - 1]  # Time step size
        inival = sol.u[istep - 1]
        solution = sol.u[istep]

        # get I-V data
        current = get_current_val(ctsys, solution, inival, Δt)

        IntSRH = integrate(ctsys, SRHRecombination!, solution)
        IntRad = integrate(ctsys, RadiativeRecombination!, solution)

        IntSRHnSum = 0.0; IntRadnSum = 0.0
        IntSRHpSum = 0.0; IntRadpSum = 0.0

        for ii in 1:p.numberOfRegions
            IntSRHnSum = IntSRHnSum - IntSRH[p.iphin, ii]
            IntRadnSum = IntRadnSum - IntRad[p.iphin, ii]

            IntSRHpSum = IntSRHpSum + IntSRH[p.iphip, ii]
            IntRadpSum = IntRadpSum + IntRad[p.iphip, ii]
        end

        push!(IV, current)
        push!(ISRHn, IntSRHnSum); push!(ISRHp, IntSRHpSum)
        push!(IRadn, IntRadnSum); push!(IRadp, IntRadpSum)

        inival = solution

    end # time loop

    if test == false
        println(&quot;*** done\n&quot;)
    end

    # here in res the biasValues and the corresponding current are stored.
    # res = [biasValues IV];

    if plotting
        Plotter.figure()
        plot_energies(Plotter, ctsys, solution, &quot;bias \$\\Delta u\$ = $(endVoltage)&quot;, label_energy)
        Plotter.figure()
        plot_densities(Plotter, ctsys, solution, &quot;bias \$\\Delta u\$ = $(endVoltage)&quot;, label_density)
        Plotter.figure()
        plot_solution(Plotter, ctsys, solution, &quot;bias \$\\Delta u\$ = $(endVoltage)&quot;, label_solution)
    end

    biasValues = contactVoltageFunction[p.bregionAcceptor].(tvalues)

    if plotting
        Plotter.figure()
        Plotter.plot(tvalues, biasValues, marker = &quot;o&quot;)
        Plotter.xlabel(&quot;time [s]&quot;)
        Plotter.ylabel(&quot;bias [V]&quot;)
        Plotter.figure()
        plot_IV(Plotter, biasValues[2:end], IV, &quot;bias \$\\Delta u\$ = $(endVoltage)&quot;)
        ###############
        Plotter.figure()
        semilogy(biasValues[2:end], ISRHn .* (cm^2) .* 1.0e3, linewidth = 5, color = &quot;darkblue&quot;, label = &quot;SRH recombination&quot;)
        semilogy(biasValues[2:end], ISRHp .* (cm^2) .* 1.0e3, linewidth = 5, color = &quot;lightblue&quot;, linestyle = &quot;:&quot;)
        semilogy(biasValues[2:end], IRadn .* (cm^2) .* 1.0e3, linewidth = 5, color = &quot;darkgreen&quot;, label = &quot;Radiative recombination&quot;)
        semilogy(biasValues[2:end], IRadp .* (cm^2) .* 1.0e3, linewidth = 5, color = &quot;lightgreen&quot;, linestyle = &quot;:&quot;)

        PyPlot.grid()
        PyPlot.legend()
        PyPlot.xlabel(&quot;bias [V]&quot;)
        PyPlot.ylabel(&quot;current density [mAcm\$^{-2} \$]&quot;)
    end

    if test == false
        integral = integrated_density(ctsys, sol = solution, icc = p.iphia, ireg = p.regionIntrinsic)

        println(&quot;Calculated average vacancy density is: &quot;, integral / data.regionVolumes[p.regionIntrinsic])
        println(&quot; &quot;)
        if vacancyEnergyCalculation
            vacancyEnergy = data.params.bandEdgeEnergy[p.iphia, p.regionIntrinsic] / data.constants.q
            println(&quot;Value for vacancy energy is: &quot;, vacancyEnergy, &quot; eV. Save this value for later use.&quot;)
            println(&quot;We recommend to calculate it on a fine grid.&quot;)
            println(&quot; &quot;)
        end
    end

    if otherScanProtocol
        return IV[1]
    else
        return sum(IV)
    end

end #  main

function test()
    testval = 313.58311884281136; testvalOther = 0.004948787599489832
    @show main(test = true, otherScanProtocol = false)
    @show main(test = true, otherScanProtocol = true, vacancyEnergyCalculation = false)
    return main(test = true, otherScanProtocol = false) ≈ testval &amp;&amp; abs(main(test = true, otherScanProtocol = true, vacancyEnergyCalculation = false) - testvalOther) / testvalOther &lt; 1.0e-7
end

if test == false
    println(&quot;This message should show when this module is successfully recompiled.&quot;)
end

end # module</code></pre><hr/><p><em>This page was generated using <a href="https://github.com/fredrikekre/Literate.jl">Literate.jl</a>.</em></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../Ex102_PIN_nodal_doping/">« GaAs diode with spatially varying doping (1D).</a><a class="docs-footer-nextpage" href="../Ex104_PSC_Photogeneration/">PSC device with photogeneration rate (1D). »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="auto">Automatic (OS)</option><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option><option value="catppuccin-latte">catppuccin-latte</option><option value="catppuccin-frappe">catppuccin-frappe</option><option value="catppuccin-macchiato">catppuccin-macchiato</option><option value="catppuccin-mocha">catppuccin-mocha</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 1.16.1 on <span class="colophon-date" title="Wednesday 10 December 2025 15:51">Wednesday 10 December 2025</span>. Using Julia version 1.12.2.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
